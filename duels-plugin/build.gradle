clean.doFirst {
    delete "$rootDir/out/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        expand(VERSION: project.version) // replaces ${VERSION} in files
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:22.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    implementation 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    implementation 'com.mojang:authlib:1.5.21'
    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly 'com.github.sirblobman.api:core:2.9-SNAPSHOT'
    compileOnly 'com.github.sirblobman.combatlogx:api:11.4-SNAPSHOT'

    implementation('net.essentialsx:EssentialsX:2.21.2') {
        transitive = false
    }
    implementation files("$rootDir/libs/Vault-1.6.7.jar")
    implementation files("$rootDir/libs/CombatTagPlus.jar")
    implementation files("$rootDir/libs/AntiRelog-3.0.11.jar")
    implementation files("$rootDir/libs/PvPManager-3.7.16.jar")
    implementation files("$rootDir/libs/Factions-1.6.9.5-U0.1.14.jar")
    implementation files("$rootDir/libs/MassiveCore.jar")
    implementation files("$rootDir/libs/Factions.jar")
    implementation files("$rootDir/libs/MyPet-2.3.4.jar")
    implementation files("$rootDir/libs/BountyHunters-2.2.6.jar")
    implementation files("$rootDir/libs/SimpleClans-2.14.4.1.jar")
    implementation files("$rootDir/libs/LeaderHeadsAPI.jar")
    implementation files("$rootDir/libs/MVdWPlaceholderAPI-3.1.1.jar")

    // Project modules
    implementation project(':duels-api')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-api'))
        include(project(':duels-worldguard'))
        include(project(':duels-worldguard-v6'))
        include(project(':duels-worldguard-v7'))
        include(dependency('com.fasterxml.jackson.core:.*'))
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson', group + 'jackson'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}
