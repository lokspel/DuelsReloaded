clean {
    delete "$rootDir/out/"
}


processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:22.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    implementation 'org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT'
    implementation 'com.mojang:authlib:1.5.21'
    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly 'com.github.sirblobman.api:core:2.9-SNAPSHOT'
    compileOnly("com.github.sirblobman.combatlogx:api:11.6-SNAPSHOT")
    implementation ('net.essentialsx:EssentialsX:2.21.2') {
        transitive = false
    }
    compileOnly files('../libs/MVdWPlaceholderAPI-3.1.1.jar')
    compileOnly files('../libs/Vault-1.6.7.jar')
    compileOnly files('../libs/CombatTagPlus.jar')
    compileOnly files('../libs/antirelog-3.0.11.jar')
    compileOnly files('../libs/PvPManager-3.7.16.jar')
    compileOnly files('../libs/Factions-1.6.9.5-U0.1.14.jar')
    compileOnly files('../libs/MassiveCore.jar')
    compileOnly files('../libs/Factions.jar')
    compileOnly files('../libs/MyPet-3.13.6.jar')
    compileOnly files('../libs/BountyHunters-2.2.6.jar')
    compileOnly files('../libs/SimpleClans-2.14.4.1.jar')
    compileOnly files('../libs/LeaderHeadsAPI.jar')

    implementation project(':duels-api')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-api'))
        include(project(':duels-worldguard'))
        include(project(':duels-worldguard-v6'))
        include(project(':duels-worldguard-v7'))
        include(dependency('com.fasterxml.jackson.core:.*'))
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson', group + 'jackson'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}